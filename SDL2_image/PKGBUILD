pkgname=ps4-openorbis-sdl2_image
pkgver=2.6.3
pkgrel=1
pkgdesc="SDL2 image loading library."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip !debug libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
depends=(
  'ps4-openorbis-sdl2'
  'ps4-openorbis-libwebp'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz")
sha256sums=('931c9be5bf1d7c8fae9b7dc157828b7eee874e23c7f24b44ba7eff6b4836312c')
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd SDL2_image-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir build && cd build

  openorbis-cmake -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr -DCMAKE_C_COMPILER_WORKS=true -DSDL2IMAGE_SAMPLES=OFF -DSDL2IMAGE_BACKEND_STB=ON -SDL2IMAGE_WEBP_SHARED=ON ..

  make
}

package() {
  cd SDL2_image-$pkgver/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  mkdir -p ${pkgdir}/${OPENORBIS}/usr/lib/pkgconfig
  echo "Requires.private: libwebp" >> "${pkgdir}/${OPENORBIS}/usr/lib/pkgconfig/SDL2_image.pc"
}
