# Maintainer: PwLDev <pwldev@purriau.com>

pkgname=ps4-openorbis-sdl2_gfx
pkgver=1.0.4
pkgrel=1
pkgdesc="SDL2 graphics drawing primitives."
arch=("any")
url="https://libsdl.org"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=("ps4-openorbis-pkg-config" "ps4-openorbis-vars")
depends=("ps4-openorbis-sdl2")
source=("http://www.ferzkopp.net/Software/SDL2_gfx/SDL2_gfx-$pkgver.tar.gz")
sha256sums=("63e0e01addedc9df2f85b93a248f06e8a04affa014a835c2ea34bfe34e576262")
groups=(
  "pacbrew" "pacbrew-ps4" "pacbrew-ps4-portlibs"
  "ps4-openorbis-portlibs" # for backward compatibility
)

build() {
  cd SDL2_gfx-$pkgver
  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" \
    --enable-mmx=no \
    --host=x86_64-freebsd12 \
    --disable-shared --enable-static \
    --with-sdl-prefix=${OPENORBIS}/usr

  make
}

package() {
  cd SDL2_gfx-$pkgver
  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install
}